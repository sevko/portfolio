/**
 * Chip name: RAM512
 * Inputs: in[16], address[9], load
 * Outputs: out[16]
 * Function: out(t) = RAM[address(t)](t)
 *      If load(t - 1) then RAM[address(t - 1)](t) = in(t - 1)
 * Comment: "=" is a 16-bit operation.
 */

CHIP RAM512 {
	IN in[16], load, address[9];
	OUT out[16];

	PARTS:
	DMux8Way(
		in=load, sel=address[6..8], a=load1, b=load2, c=load3, d=load4, e=load5,
		f=load6, g=load7, h=load8
	);

	RAM64(in=in, load=load1, address=address[0..5], out=ram1);
	RAM64(in=in, load=load2, address=address[0..5], out=ram2);
	RAM64(in=in, load=load3, address=address[0..5], out=ram3);
	RAM64(in=in, load=load4, address=address[0..5], out=ram4);
	RAM64(in=in, load=load5, address=address[0..5], out=ram5);
	RAM64(in=in, load=load6, address=address[0..5], out=ram6);
	RAM64(in=in, load=load7, address=address[0..5], out=ram7);
	RAM64(in=in, load=load8, address=address[0..5], out=ram8);

	Mux8Way16(
		a=ram1, b=ram2, c=ram3, d=ram4, e=ram5, f=ram6, g=ram7, h=ram8,
		sel=address[6..8], out=out
	);
}
